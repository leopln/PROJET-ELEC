#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>
#include <EEPROM.h>

Adafruit_SSD1306 display(128, 64, &Wire, -1);
#define PIN_CLK 3
#define PIN_DT  4
#define PIN_SW  2
#define PIN_BUZZER 10

int canal = 1;
int son = 1;
int pseudoIndex = 0;

const char* listePseudos[] = {"Editeur1", "Editeur2", "Editeur3", "Editeur4", "Editeur5"};

int etatMenu = 0;
int curseur = 1;

void setup() {
  Serial.begin(9600);
  
  display.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  
  pinMode(PIN_CLK, INPUT_PULLUP);
  pinMode(PIN_DT, INPUT_PULLUP);
  pinMode(PIN_SW, INPUT_PULLUP);
  pinMode(PIN_BUZZER, OUTPUT);

  canal = EEPROM.read(0);
  if (canal == 255) canal = 1;

  pseudoIndex = EEPROM.read(1);
  if (pseudoIndex == 255) pseudoIndex = 0;

  son = EEPROM.read(2);
  if (son == 255) son = 1;
}

void loop() {
  if (etatMenu == 0) {
    afficherMenuPrincipal();
  } else if (etatMenu == 1) {
    reglerCanal();
  } else if (etatMenu == 2) {
    reglerPseudo();
  } else if (etatMenu == 3) {
    reglerSon();
  }
}

void afficherMenuPrincipal() {
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println(F("--- MENU REGLAGES ---"));
  
  if (curseur == 1) display.print(F("> ")); else display.print(F("  "));
  display.println(F("Canal Radio"));
  
  if (curseur == 2) display.print(F("> ")); else display.print(F("  "));
  display.println(F("Pseudo"));
  
  if (curseur == 3) display.print(F("> ")); else display.print(F("  "));
  display.println(F("Sonnerie"));
  
  if (curseur == 4) display.print(F("> ")); else display.print(F("  "));
  display.println(F("Quitter"));

  display.display();

  int action = lireEncodeur();
  if (action == 1) { curseur++; if (curseur > 4) curseur = 1; }
  if (action == -1) { curseur--; if (curseur < 1) curseur = 4; }
  
  if (action == 2) {
    if (curseur == 1) etatMenu = 1;
    if (curseur == 2) etatMenu = 2;
    if (curseur == 3) etatMenu = 3;
    if (curseur == 4) {
      display.clearDisplay();
      display.setCursor(0,20);
      display.print("Sauvegarde OK!");
      display.display();
      delay(1000);
    }
  }
}

void reglerCanal() {
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println(F("CHOIX CANAL (0-100)"));
  display.setTextSize(2);
  display.setCursor(50, 30);
  display.print(canal);
  display.setTextSize(1);
  display.display();

  int action = lireEncodeur();
  if (action == 1 && canal < 100) canal++;
  if (action == -1 && canal > 0) canal--;
  
  if (action == 2) {
    EEPROM.write(0, canal);
    etatMenu = 0;
  }
}

void reglerPseudo() {
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println(F("CHOIX PSEUDO"));
  
  display.setTextSize(2);
  display.setCursor(20, 30);
  display.print(listePseudos[pseudoIndex]);
  display.setTextSize(1);
  display.display();

  int action = lireEncodeur();
  if (action == 1 && pseudoIndex < 4) pseudoIndex++;
  if (action == -1 && pseudoIndex > 0) pseudoIndex--;
  
  if (action == 2) {
    EEPROM.write(1, pseudoIndex);
    etatMenu = 0;
  }
}

void reglerSon() {
  display.clearDisplay();
  display.setCursor(0, 0);
  display.println(F("TYPE SONNERIE (1-3)"));
  display.setTextSize(2);
  display.setCursor(60, 30);
  display.print(son);
  display.setTextSize(1);
  display.display();

  int action = lireEncodeur();
  if (action == 1 && son < 3) { son++; bipTest(); }
  if (action == -1 && son > 1) { son--; bipTest(); }
  
  if (action == 2) {
    EEPROM.write(2, son);
    etatMenu = 0;
  }
}

void bipTest() {
  if (son == 1) { tone(PIN_BUZZER, 1000, 100); }
  if (son == 2) { tone(PIN_BUZZER, 2000, 100); }
  if (son == 3) { tone(PIN_BUZZER, 500, 100); }
}

int lastClk = HIGH;
int lireEncodeur() {
  int resultat = 0;
  int currentClk = digitalRead(PIN_CLK);

  if (currentClk != lastClk && currentClk == LOW) {
    if (digitalRead(PIN_DT) != currentClk) resultat = 1;
    else resultat = -1;
  }
  lastClk = currentClk;

  if (digitalRead(PIN_SW) == LOW) {
    delay(200);
    resultat = 2; 
  }
  return resultat;
}
